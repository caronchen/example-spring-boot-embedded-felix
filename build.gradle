import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	ext {
        springBootVersion = '2.0.3.RELEASE'
        springDependencyManagementVersion = '1.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
	id 'java' apply true
	id 'eclipse' apply true
	id 'org.springframework.boot' version '2.0.3.RELEASE' apply true
	id 'io.spring.dependency-management' version '1.0.5.RELEASE' apply true
	id 'application' apply true
}

//defaultTasks 'build','copyFrameworkBundles'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
group = 'com.example'
version = '0.0.1'
mainClassName = 'com.example.App'

configurations {
    framework_bundles
}

bootJar {
	launchScript()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile('org.apache.felix:org.apache.felix.framework:6.0.0')
	compile('org.apache.felix:org.apache.felix.main:6.0.0')
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
	
	framework_bundles "org.apache.felix:org.apache.felix.fileinstall:3.6.4"
	framework_bundles "org.apache.felix:org.apache.felix.configadmin:1.9.2"
	framework_bundles "org.apache.felix:org.apache.felix.scr:2.1.0"
	framework_bundles "org.apache.felix:org.apache.felix.eventadmin:1.5.0"
	framework_bundles "org.apache.felix:org.apache.felix.http.jetty:4.0.0"
	framework_bundles "org.apache.felix:org.apache.felix.http.servlet-api:1.1.2"
	
	//framework_bundles "org.apache.felix:org.apache.felix.gogo.command:1.0.2"
	//framework_bundles "org.apache.felix:org.apache.felix.gogo.runtime:1.1.0"
	//framework_bundles "org.apache.felix:org.apache.felix.gogo.shell:1.1.0"
	
	
}

def distributionsCopySpec = copySpec {

	def distroName = "$name-$version";
	
    into(distroName) {
    	from 'src/main/dist'
        into('lib') {
            from libsDir
            include '*.jar' //copy the fat jar created by bootRepackage
        }
        into('framework_bundles') {
            from configurations.framework_bundles
        }
        into('bin') {
            from 'src/main/templates'
		    include 'start.*.template'
			filter(ReplaceTokens, tokens: [
				name: project.name,
				extra_java_opts_property: project.name.replace('-','_').toUpperCase() + "_OPTS",
				extra_java_opts: '-Dspring.config.location=$APP_HOME/conf/',
				version: project.version,
				generated: new Date().toString()
			])
			fileMode = 0766
        }
        into('conf') {
            from "src/main/resources/application.properties" //custom start script based on startScripts task output
        }
        rename ("^start.sh.template\$","start.sh")
        fileMode = 0755
        rename ("^start.bat.template\$","start.bat")
		
    }
}


task fatJarZip(type: Zip, dependsOn: 'assembleBootDist') { with distributionsCopySpec }
task fatJarTar(type: Tar, dependsOn: 'assembleBootDist') { with distributionsCopySpec }
fatJarZip.group = "custom"
fatJarTar.group = "custom"